#!/usr/local/bin/perl
# Add index anchors to source file _and_ generate index database
# Arnaud Le Hors - lehors@w3.org
# $Id: addianch,v 2.0 1998-02-02 18:52:27 bbos Exp $

use Getopt::Std;
use lib 'bin';
use utils;

$PROG = substr($0, rindex($0, "/") + 1);
$USAGE = "Usage: $PROG [-r url-prefix] dbase [file [output]]\n";

# Remove A tags, initial and trailing ws, collapse all ws to single space
sub cleanup {
    my $h = $_[0];
    $h =~ tr/\n\t/ /s;		# newline & tab -> spaces, and collapse
    $h =~ s/<\/?a\b.*?>//gio;	# remove A tags
    $h =~ s/^ +//o;		# remove initial space
    $h =~ s/ +$//o;		# remove trailing space
    return $h;
}

# After matching, $1.$3=part of start tag w/o class, $4=content, $2=class
$pre = '<span\s+([^>]*?)class\s*=\s*[\"\']?';
$post = '[\"\']?([^>]*)>(.*?)</span\s*>';
$defp = $pre.'(index-def|index-inst)'.$post;

### main

getopts('r:') || die $USAGE;
if ($#ARGV >= 0) {$db = $ARGV[0]; shift;} else {die $USAGE;}
if ($#ARGV >= 0) {$file = $ARGV[0]; shift;} else {$file = '-';}
if ($#ARGV >= 0) {$output = $ARGV[0]; shift;} else {$output = '-';}
if ($#ARGV >= 0) {die $USAGE;}

$prefix = defined $opt_r ? $opt_r : $file; # URL prefix

dbmopen(%anchors, $db, 0666) || die "$PROG: cannot open database $db\n";

$buf = readfile($file);		# Load file
$buf =~ s/<!--.*?-->//gio;	# Remove comments

open(OUTPUT, ">$output") || die "$PROG: cannot create file $output\n";

$seqno = 0;

while ($buf =~ /$defp/sio) {
    print OUTPUT $`;
    $buf = $';
    $elem = $&;
    $content = $3;
    $class = $2;

    # Check if there is a title attribute, otherwise use content
    $h = $1.$3;
    if ($h =~ /title\s*=\s*(?:\"([^\"]*)\"|\'([^\']*)\'|([^\s>]*))/sio) {
	$entry = $1.$2.$3;	# Only one of the three is non-empty
    } else {
	$entry = $content;	# Assume content is index term
    }
    # Split entry in case there are several
    @entries = split(/\|/o, cleanup($entry));

    # Create an anchor
    if ($content =~ /<a\b[^>]*?name\s*=\s*[\"\']?([^\s\"\'>]*?)[\"\']?/sio) {
	# Already a name in the content, re-use it
	$anchor = $1;
	print OUTPUT $elem;
    } else {
	# Create a unique anchor
	$anchor = "x$seqno";
	print OUTPUT "<a name=\"$anchor\">$elem</a>";
    }

    # Put URL in database: url-prefix class anchor entry
    foreach $e (@entries) {
	$key = "$prefix\t$seqno";
	$h = "$e\t$prefix\t$class\t$anchor";
	$anchors{$key} = $h;
	$seqno++;
    }
}
print OUTPUT $buf;		# Print final part of file

close(OUTPUT);
dbmclose(%anchors);
